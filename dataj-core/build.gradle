buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'signing'

if (project.hasProperty('release')) {
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { signing.signPom(it) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
            pom.project {
                name project.name
                description project.description
                packaging 'jar'
                url 'http://dataj.org'

                scm {
                    connection 'scm:git:https://github.com/alek-sys/dataj.git'
                    developerConnection 'scm:git:git@github.com:alek-sys/dataj.git'
                    url 'https://github.com/alek-sys/dataj.git'
                }

                licenses {
                    license {
                        name 'Apache License 2.0'
                        url 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'alexeynesterov'
                        name = 'Alexey Nesterov'
                        email = 'email@alexnesterov.com'
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
    packageGroup = 'org.dataj'
}

group = 'org.dataj'
version = 0.1

ext {
    autoServiceVersion = '1.0-rc3'
    junitPlatformVersion = '1.0.0'
    junitJupiterVersion  = '5.0.0'
}

dependencies {
    api 'com.squareup:javapoet:1.9.0'
    api 'com.google.guava:guava:22.0'
    implementation "com.google.auto.service:auto-service:$autoServiceVersion"

    testCompile 'com.squareup:javapoet:1.9.0'
    testCompile 'com.google.testing.compile:compile-testing:0.12'
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testCompile 'com.github.javaparser:javaparser-core:3.3.4'
    testCompile 'com.google.code.findbugs:jsr305:3.0.2'

    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testRuntime "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"
}